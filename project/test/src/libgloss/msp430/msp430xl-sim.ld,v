head	1.4;
access;
symbols
	newlib-2_2_0:1.4.0.2
	newlib-2_1_0:1.2;
locks; strict;
comment	@# @;


1.4
date	2014.11.13.10.18.57;	author nickc;	state Exp;
branches;
next	1.3;

1.3
date	2014.01.30.02.46.34;	author dj;	state Exp;
branches;
next	1.2;

1.2
date	2013.09.18.07.38.13;	author nickc;	state Exp;
branches;
next	1.1;

1.1
date	2013.05.13.21.39.51;	author dj;	state Exp;
branches;
next	;


desc
@@


1.4
log
@	* msp430/Makefile.in (CRT_OBJS): Add crt_high_bss.o.
	* msp430/crt0.S (high_bss): Add.
	* msp430/msp430-sim.ld: Add error message if .upper sections are
	detected.
	* msp430/msp430xl-sim.ld (MEMORY): Adjust to better mimic real
	life MCUs.  Add support for upper and lower sections.
@
text
@/*  Copyright (c) 2013-2014 Red Hat, Inc. All rights reserved.

   This copyrighted material is made available to anyone wishing to use, modify,
   copy, or redistribute it subject to the terms and conditions of the BSD
   License.   This program is distributed in the hope that it will be useful,
   but WITHOUT ANY WARRANTY expressed or implied, including the implied warranties
   of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  A copy of this license
   is available at http://www.opensource.org/licenses. Any Red Hat trademarks that
   are incorporated in the source code or documentation are not subject to the BSD
   License and may only be used or replicated with the express permission of
   Red Hat, Inc.  */

/* Default linker script, for large MSP430X executables.  */

OUTPUT_ARCH(msp430)
ENTRY(_start)

INCLUDE intr_vectors.ld

MEMORY
{
  RAM (w)      : ORIGIN = 0x00200, LENGTH = 0x01e00
  ROM (rx)     : ORIGIN = 0x02000, LENGTH = 0x0df00
  HIFRAM (rxw) : ORIGIN = 0x10000, LENGTH = 0x80000
}

SECTIONS
{
  .resetvec :
  {
    *(.resetvec)
  } > VECT31

  .upper.rodata :
  {
    . = ALIGN(2);
    *(.upper.rodata.* .upper.rodata)
  } > HIFRAM

  .rodata :
  {
    . = ALIGN(2);
    *(.plt)
    *(.lower.rodata.* .lower.rodata)
    *(.rodata .rodata.* .gnu.linkonce.r.* .const .const:*)
    *(.rodata1)

    *(.eh_frame_hdr)
    KEEP (*(.eh_frame))
    KEEP (*(.gcc_except_table)) *(.gcc_except_table.*)
    PROVIDE (__preinit_array_start = .);
    KEEP (*(.preinit_array))
    PROVIDE (__preinit_array_end = .);
    PROVIDE (__init_array_start = .);
    KEEP (*(SORT(.init_array.*)))
    KEEP (*(.init_array))
    PROVIDE (__init_array_end = .);
    PROVIDE (__fini_array_start = .);
    KEEP (*(.fini_array))
    KEEP (*(SORT(.fini_array.*)))
    PROVIDE (__fini_array_end = .);
    LONG(0); /* Sentinel.  */

    /* gcc uses crtbegin.o to find the start of the constructors, so
       we make sure it is first.  Because this is a wildcard, it
       doesn't matter if the user does not actually link against
       crtbegin.o; the linker won't look for a file to match a
       wildcard.  The wildcard also means that it doesn't matter which
       directory crtbegin.o is in.  */
    KEEP (*crtbegin*.o(.ctors))

    /* We don't want to include the .ctor section from from the
       crtend.o file until after the sorted ctors.  The .ctor section
       from the crtend file contains the end of ctors marker and it
       must be last */
    KEEP (*(EXCLUDE_FILE (*crtend*.o ) .ctors))
    KEEP (*(SORT(.ctors.*)))
    KEEP (*(.ctors))

    KEEP (*crtbegin*.o(.dtors))
    KEEP (*(EXCLUDE_FILE (*crtend*.o ) .dtors))
    KEEP (*(SORT(.dtors.*)))
    KEEP (*(.dtors))
  } > ROM

  .upper.data :
  {
    . = ALIGN(2);
    *(.upper.data.* .upper.data)
  } > HIFRAM

  .data :
  {
    . = ALIGN(2);
    PROVIDE (__datastart = .);
    *(.lower.data.* .lower.data)

    . = ALIGN(2);
    KEEP (*(.jcr))
    *(.data.rel.ro.local) *(.data.rel.ro*)
    *(.dynamic)

    *(.data .data.* .gnu.linkonce.d.*)
    KEEP (*(.gnu.linkonce.d.*personality*))
    SORT(CONSTRUCTORS)
    *(.data1)
    *(.got.plt) *(.got)

    /* We want the small data sections together, so single-instruction offsets
       can access them all, and initialized data all before uninitialized, so
       we can shorten the on-disk segment size.  */
    . = ALIGN(2);
    *(.sdata .sdata.* .gnu.linkonce.s.* D_2 D_1)

    . = ALIGN(2);

    _edata = .;
    PROVIDE (edata = .);
    PROVIDE (__dataend = .);
  } > RAM

  /* Note that crt0 assumes this is a multiple of two; all the
     start/stop symbols are also assumed word-aligned.  */
  PROVIDE(__romdatastart = LOADADDR(.data));
  PROVIDE (__romdatacopysize = SIZEOF(.data));

  .upper.bss :
  {
    . = ALIGN(2);
    PROVIDE (__high_bssstart = .);
    *(.upper.bss.* .upper.bss)
    PROVIDE (__high_bssend = .);
  } > HIFRAM

  .bss :
  {
    . = ALIGN(2);
    PROVIDE (__bssstart = .);
    *(.lower.bss.* .lower.bss)
    *(.dynbss)
    *(.sbss .sbss.*)
    *(.bss .bss.* .gnu.linkonce.b.*)
    . = ALIGN(2);
    PROVIDE (__bssend = .);
  } > RAM
  PROVIDE (__bsssize = SIZEOF(.bss));

  /* This section contains data that is not initialised at startup.  */
  .noinit (NOLOAD) :
  {
    . = ALIGN(2);
    PROVIDE (__noinit_start = .);
    *(.noinit)
    . = ALIGN(2);
    *(COMMON)
    PROVIDE (__noinit_end = .);
  } > RAM

  _end = .;
  PROVIDE (end = .);

  .stack (ORIGIN (HIFRAM) + LENGTH(HIFRAM)) :
  {
    PROVIDE (__stack = .);
    *(.stack)
  }

  /* This is just for crt0.S and interrupt handlers.  */
  .lowtext :
  {
    PROVIDE (_start = .);
    . = ALIGN(2);
    KEEP (*(SORT(.crt_*)))
    KEEP (*(.lowtext))
  } > ROM

  .upper.text :
  {
    . = ALIGN(2);
    *(.upper.text.* .upper.text)
  } > HIFRAM

  .text :
  {
    . = ALIGN(2);
    *(.lower.text.* .lower.text)
    . = ALIGN(2);
    *(.text .stub .text.* .gnu.linkonce.t.* .text:*)

    KEEP (*(.text.*personality*))
    /* .gnu.warning sections are handled specially by elf32.em.  */
    *(.gnu.warning)
    *(.interp .hash .dynsym .dynstr .gnu.version*)
    PROVIDE (__etext = .);
    PROVIDE (_etext = .);
    PROVIDE (etext = .);
    . = ALIGN(2);
    KEEP (*(.init))
    KEEP (*(.fini))
    KEEP (*(.tm_clone_table))
  } > ROM

  /* The rest are all not normally part of the runtime image.  */

  .MP430.attributes 0 :
  {
    KEEP (*(.MSP430.attributes))
    KEEP (*(.gnu.attributes))
    KEEP (*(__TI_build_attributes))
  }

  /* Stabs debugging sections.  */
  .stab          0 : { *(.stab) }
  .stabstr       0 : { *(.stabstr) }
  .stab.excl     0 : { *(.stab.excl) }
  .stab.exclstr  0 : { *(.stab.exclstr) }
  .stab.index    0 : { *(.stab.index) }
  .stab.indexstr 0 : { *(.stab.indexstr) }
  .comment       0 : { *(.comment) }
  /* DWARF debug sections.
     Symbols in the DWARF debugging sections are relative to the beginning
     of the section so we begin them at 0.  */
  /* DWARF 1.  */
  .debug          0 : { *(.debug) }
  .line           0 : { *(.line) }
  /* GNU DWARF 1 extensions.  */
  .debug_srcinfo  0 : { *(.debug_srcinfo) }
  .debug_sfnames  0 : { *(.debug_sfnames) }
  /* DWARF 1.1 and DWARF 2.  */
  .debug_aranges  0 : { *(.debug_aranges) }
  .debug_pubnames 0 : { *(.debug_pubnames) }
  /* DWARF 2.  */
  .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
  .debug_abbrev   0 : { *(.debug_abbrev) }
  .debug_line     0 : { *(.debug_line .debug_line.* .debug_line_end ) }
  .debug_frame    0 : { *(.debug_frame) }
  .debug_str      0 : { *(.debug_str) }
  .debug_loc      0 : { *(.debug_loc) }
  .debug_macinfo  0 : { *(.debug_macinfo) }
  /* SGI/MIPS DWARF 2 extensions.  */
  .debug_weaknames 0 : { *(.debug_weaknames) }
  .debug_funcnames 0 : { *(.debug_funcnames) }
  .debug_typenames 0 : { *(.debug_typenames) }
  .debug_varnames  0 : { *(.debug_varnames) }
  /* DWARF 3 */
  .debug_pubtypes 0 : { *(.debug_pubtypes) }
  .debug_ranges   0 : { *(.debug_ranges) }
  /* DWARF Extension.  */
  .debug_macro    0 : { *(.debug_macro) }

  /DISCARD/ : { *(.note.GNU-stack) }
}
@


1.3
log
@* msp430/Makefile.in (crt0-minrt.o, crtn-minrt.o): New.  Build
from crt0.S with -DMINRT.
(CRT_OBJS): Expand.
(crt_%.o): New rule pattern.  Build multiple objects from crt0.S.
* msp430/crt0.S: Further break out functionality.  Support -DMINRT
that omits all init/fini logic.
* msp430/crtn.S: Likewise.
* msp430/msp430-sim.ld: Wildcard all .crt_* sections, sorted.
* msp430/msp430.ld: Likewise.
* msp430/msp430xl-sim.ld: Likewise.
@
text
@d1 1
a1 1
/*  Copyright (c) 2013 Red Hat, Inc. All rights reserved.
d11 1
a11 1
   Red Hat, Inc.
d13 1
a13 1
*/
a14 1
/* Default linker script, for normal executables */
a17 3
/* Do we need any of these for elf?
   __DYNAMIC = 0;    */

d20 5
a24 5
MEMORY {
	RAM (rx)     : ORIGIN = 0x00200, LENGTH = 0x0ee00
	LOWROM (w)    : ORIGIN = 0x0f000, LENGTH = 0x00800
	ROM (w)      : ORIGIN = 0x10000, LENGTH = 0x80000
	RESETVEC (w) : ORIGIN = 0x0fffe, LENGTH = 0x00002
d32 1
a32 1
  } > RESETVEC
d34 8
a41 1
  .rodata : {
d44 1
d47 1
d86 8
a93 1
  .data : {
d96 1
d98 1
d116 1
d120 1
a120 1
  } > RAM AT>ROM
d127 10
a136 1
  .bss : {
d139 1
d149 2
a150 1
  .noinit : {
d162 1
a162 1
  .stack (ORIGIN (RAM) + LENGTH(RAM)) :
d168 2
a169 2
  /* This is just for crt0.S */
  .lowtext           :
d175 7
a181 1
  } > LOWROM
d183 1
a183 1
  .text           :
d186 2
d189 1
d223 1
a223 1
  /* DWARF 1 */
d226 1
a226 1
  /* GNU DWARF 1 extensions */
d229 1
a229 1
  /* DWARF 1.1 and DWARF 2 */
d232 1
a232 1
  /* DWARF 2 */
d240 1
a240 1
  /* SGI/MIPS DWARF 2 extensions */
@


1.2
log
@	* msp430/Makefile.in (SCRIPTS): Add intr_vectors.ld.
	(LIB_CRT): New.
	(CRT_OBJS): New.
	(all): Add dependency upon LIB_CRT.
	(install): Likewise.
	* msp430/msp430-sim.ld: Include intr_vectors.ld.
	KEEP crt code, lowtext code and tm_clone_table.
	Separate the noinit section from the .bss section.
	Allow for extended .debug_line sections.
	* msp430/msp430.ld: Likewise.
	* msp430/msp430F5438A-l.ld: Likewise.
	* msp430/msp430F5438A-s.ld: Likewise.
	* msp430/msp430xl-sim.ld: Likewise
.	* msp430/crt_movedata.S: New.
	* msp430/crt_bss.S: New.
	* msp430/intr_vectors.ld: New.
@
text
@d147 1
a147 4
    KEEP (*(.crt_init))
    KEEP (*(.crt_bss))
    KEEP (*(.crt_movedata))
    KEEP (*(.crt_main))
@


1.1
log
@[newlib]
* configure.host (msp430): Add.
* libc/include/machine/ieeefp.h: Add MSP430 support.
* libc/include/machine/setjmp.h: Likewise.
* libc/include/sys/config.h: Likewise.
* libc/machine/configure.in (msp430): Add.
* libc/machine/configure: Regenerate.
* libc/machine/msp430: New directory.

[libgloss]
* configure.in (msp430*-*-elf): Add.
* configure: Regenerate.
* msp430: New directory.
@
text
@d22 2
a118 2
    *(COMMON)
    . = ALIGN(2);
a119 2
    _end = .;
    PROVIDE (end = .);
d123 13
d147 5
a151 1
    *(.lowtext)
d153 1
d168 1
d203 1
a203 1
  .debug_line     0 : { *(.debug_line) }
@

